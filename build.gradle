plugins {
    id 'multiloader-root'
    id 'java'
    id 'java-library'
    id 'net.neoforged.moddev'           version "${neoforged_moddev}"            apply false
    id 'fabric-loom'                    version "${fabric_loom}"                 apply false
    id 'io.papermc.paperweight.userdev' version "${papermc_paperweight_userdev}" apply false
    id 'xyz.jpenilla.run-paper'         version "${jpenilla_run_task}"           apply false
    id 'com.gradleup.shadow'            version "${gradleup_shadow}"             apply false
    id 'xyz.jpenilla.resource-factory'  version "${jpenilla_resource_factory}"   apply false
    id 'io.github.dexman545.outlet'     version "${dexman_outlet}"               apply false
    id 'com.modrinth.minotaur'          version "${modrinth_minotaur}"           apply false
    id 'hamburg.janove.elevator-music'  version "0.1"
}

//elevatorMusic {
//    if (Boolean.parseBoolean(System.getenv("DISABLE_MUSIC"))) return
//    waitMusic = file("${rootProject.projectDir}/veryImportant/music.wav")
//    successSound = file("${rootProject.projectDir}/veryImportant/done.wav")
//}

subprojects {
    apply plugin: "java-library"

    repositories {
        mavenCentral()
        mavenLocal()
        exclusiveContent {
            forRepository {
                maven {
                    name = "OffsetMods538"
                    url = "https://maven.offsetmonkey538.top/releases"
                }
            }
            filter {
                includeGroupAndSubgroups "top.offsetmonkey538"
            }
        }
    }

    dependencies {
        //TODO: switch to this: compileOnlyApi "org.jspecify:jspecify:${rootProject.jspecify_version}"
        compileOnlyApi "org.jetbrains:annotations:24.0.0"
    }
}

configure(subprojects.findAll { it.hasProperty("project_name") }) {
    apply plugin: "maven-publish"

    publishing {
        repositories {
            maven {
                name = "OffsetMonkey538"
                url = "https://maven.offsetmonkey538.top/releases"
                credentials {
                    username = providers.gradleProperty("OffsetMonkey538Username").getOrElse(System.getenv("MAVEN_USERNAME"))
                    password = providers.gradleProperty("OffsetMonkey538Password").getOrElse(System.getenv("MAVEN_PASSWORD"))
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            register("maven", MavenPublication) {
                afterEvaluate {
                    artifactId base.archivesName.get()
                }
                groupId "top.offsetmonkey538.meshlib"
                from components.java
            }
        }
    }
}
