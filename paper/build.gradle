import io.papermc.paperweight.userdev.ReobfArtifactConfiguration

plugins {
	id 'com.gradleup.shadow' version '9.0.0-beta4'
	id 'io.papermc.paperweight.userdev' version '2.0.0-beta.8'
	id 'xyz.jpenilla.run-paper' version '2.3.1'
	id 'maven-publish'
}

paperweight.reobfArtifactConfiguration = ReobfArtifactConfiguration.getMOJANG_PRODUCTION()

repositories {
	mavenCentral()
	maven {
		name = 'papermc'
		url = 'https://repo.papermc.io/repository/maven-public/'
	}
}

configurations {
	common {
		canBeResolved = true
		canBeConsumed = false
	}
	api.extendsFrom common
}

dependencies {
	// Paper
	compileOnly "io.papermc.paper:paper-api:${project.paper_version}"

	// Userdev
	paperweight.paperDevBundle(project.paper_version)

	// Common
	common project(path: ":common", configuration: "shadow")
}
tasks.build.dependsOn(shadowJar)

processResources {
	final Map properties = Map<String, String>.of(
			"modVersion",                 rootProject.mod_version,
			"lowestMinecraftVersion",     outlet.mcVersions().first() // Hopefully outlet always does stuff in this order
	)

	inputs.properties(properties)

	filesMatching("plugin.yml") {
		expand(properties)
	}
}

tasks.runServer {
	minecraftVersion("1.21.4")
	jvmArgs "-DmeshEnableExamples=true"
}

modrinth {
	loaders = ["paper"]
	uploadFile = shadowJar
}
