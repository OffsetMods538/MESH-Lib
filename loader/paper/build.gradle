import dex.plugins.outlet.v2.util.ReleaseType
import xyz.jpenilla.resourcefactory.paper.PaperPluginYaml

plugins {
	id 'multiloader-paper'
}

allprojects {
	// Gotta have this here cause I can't import it in buildSrc
	outlet.allowedReleaseTypes = Set.of(ReleaseType.RELEASE)

	paperPluginYaml {
		main = "top.offsetmonkey538.meshlib.paper.platform.PlatformUtilImpl\$MESHLibInitializer"
		authors.add("OffsetMonkey538")
		load = "STARTUP"
		dependencies {
			server("MonkeyLib538", PaperPluginYaml.Load.BEFORE, true, true)
		}
	}

	dependencies {
		compileOnly("top.offsetmonkey538.monkeylib538:monkeylib538-paper:${rootProject.monkeylib538_version}+paper")
	}
}

subprojects {
	dependencies {
		runtimeOnly("io.netty:netty-codec-http:${rootProject.netty_version}") {
			transitive = false
		}
	}

	tasks.runServer {
		jvmArgs "-DmeshEnableExamples=true"
		downloadPlugins {
			url("https://maven.offsetmonkey538.top/releases/top/offsetmonkey538/monkeylib538/monkeylib538-paper-${project.monkeylib538_suffix}/${rootProject.monkeylib538_version}+paper+${project.monkeylib538_suffix}/monkeylib538-paper-${project.monkeylib538_suffix}-${rootProject.monkeylib538_version}+paper+${project.monkeylib538_suffix}-all.jar")
		}
	}

	modrinth {
		dependencies {
			required.project "monkeylib538"
		}
	}
}
